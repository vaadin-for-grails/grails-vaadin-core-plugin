package org.vaadin.grails.ui.builders

import com.vaadin.data.fieldgroup.FieldGroup

/**
 * Vaadin component tree
 *
 * @author Stephan Grundner
 * @since 1.0
 */
class ComponentTree {

    static interface TreeNode {
        String getPrefix()
        TreeNode getParent()
        List<TreeNode> getChildren()
        Object getName()
        Map getAttributes()
        Object getValue()

        Object getPayload()
        void setPayload(Object payload)

        TreeNodeHandler getHandler()
        void setHandler(TreeNodeHandler handler)
    }

    static interface TreeNodeHandler {

        boolean acceptNode(TreeNode node)
        void handle(TreeNode node)
        void handleChildren(TreeNode node)
    }

    final Set<TreeNodeHandler> nodeHandlers = new LinkedHashSet<>()

    protected final def fieldGroups = new HashMap<String, FieldGroup>()
    protected Map<String, Object> components = new HashMap<String, Object>()

    ComponentTree() {
        addNodeHandler(createNodeHandler(org.vaadin.grails.ui.builders.handlers.I18nNodeHandler))
        addNodeHandler(createNodeHandler(org.vaadin.grails.ui.builders.handlers.BuildNodeHandler))
        addNodeHandler(createNodeHandler(org.vaadin.grails.ui.builders.handlers.TreeItemNodeHandler))
        addNodeHandler(createNodeHandler(org.vaadin.grails.ui.builders.handlers.TabSheetTabNodeHandler))
        addNodeHandler(createNodeHandler(org.vaadin.grails.ui.builders.handlers.AccordionTabNodeHandler))
        addNodeHandler(createNodeHandler(org.vaadin.grails.ui.builders.handlers.MenuItemNodeHandler))
        addNodeHandler(createNodeHandler(org.vaadin.grails.ui.builders.handlers.ComponentNodeHandler))
    }

    protected TreeNodeHandler createNodeHandler(Class<? extends TreeNodeHandler> nodeHandlerClass) {
        nodeHandlerClass.newInstance(this)
    }

    void addNodeHandler(TreeNodeHandler nodeHandler) {
        assert nodeHandler != null
        nodeHandlers.add(nodeHandler)
    }

    void removeNodeHandler(TreeNodeHandler nodeHandler) {
        assert nodeHandler != null
        nodeHandlers.remove(nodeHandler)
    }

    void setFieldGroup(String key, FieldGroup fieldGroup) {
        if (fieldGroups.containsKey(key) && fieldGroup == null) {
            fieldGroups.remove(key)
            return
        }
        assert fieldGroup != null
        fieldGroups.put(key, fieldGroup)
    }

    FieldGroup getFieldGroup(String key) {
        fieldGroups.get(key)
    }

    FieldGroup getDefaultFieldGroup() {
        fieldGroups.size() == 1 ? fieldGroups.values().first() : null
    }

    Object getComponent(String id) {
        components.get(id)
    }

    void clear() {
        components.clear()
    }

    /**
     * Process a node generated by a Groovy builder or an XML builder.
     *
     * @param node
     */
    public void processNode(TreeNode node) {
        def handler = nodeHandlers.find { it.acceptNode(node) }
        if (handler) {
            def attributesCopy = node.attributes?.clone()
            def id = node.attributes?.remove("id")
            try {
                node.handler = handler
                handler.handle(node)
                node.parent?.handler?.handleChildren(node.parent)
                if (id) {
                    components.put(id, node.payload)
                }
            } finally {
                if (attributesCopy) {
                    node.attributes.putAll(attributesCopy)
                }
            }
        } else {
            throw new RuntimeException("Unexpected node with name [${node.name}]" +
                    (node.prefix ? " and prefix [${node.prefix}]" : ""))
        }
    }
}
